	NAME	RANDGEN
	TITLE	RANDGEN.ASM -- Random Number Generator

CODE	SEGMENT
	ASSUME	ES:CODE, CS:CODE, SS:CODE, DS:CODE
	ORG	0100H

START:
	XOR	AX, AX
	MOV	CX, AX
	MOV	DX, AX
	MOV	CL, [DS:0080H]	; XXX: BUG?
	CALL	TRIM
	OR	CX, CX
	JZ	MAIN
	DEC	CL
	CLD
	MOV	SI, 0082H
NEXTCH:
	LODSB
	SUB	AL, 30H
	JS	INVAL
	CMP	AL, 09H
	JA	INVAL
	MOV	BX, DX
	PUSH	CX
	MOV	CL, 03H
	SHL	BX, CL
	POP	CX
	ADD	DX, DX
	ADD	DX, BX
	ADD	DX, AX
	LOOP	NEXTCH
	JMP	SHORT MAIN
INVAL:
	MOV	AH, 40H
	MOV	CX, 0007H
	MOV	DX, OFFSET EMESG1
	MOV	BX, 0002H
	INT	21H
	MOV	AH, 40H
	MOV	CL, [DS:0080H]
	DEC	CL
	MOV	DX, 0082H
	MOV	BX, 0002H
	INT	21H
	MOV	AH, 40H
	MOV	CX, 002DH
	MOV	DX, OFFSET EMESG2
	MOV	BX, 0002H
	INT	21H
	MOV	AX, 4C01H
	INT	21H
MAIN:
	PUSH	DX
	MOV	AH, 2CH
	INT	21H
	MOV	AX, CX
	XOR	AX, DX
	CALL	SEED
	POP	CX
RANDCH:
	CALL	RANDOM
	MOV	DL, AL
	MOV	AH, 02H
	INT	21H
	LOOP	RANDCH
	MOV	AX, 4C00H
	INT	21H
RANDOM:
	PUSH	CX
	PUSH	DX
	MOV	AX, XSSTAT
	MOV	DX, AX
	MOV	CL, 07H
	SHL	DX, CL
	XOR	AX, DX
	MOV	DX, AX
	INC	CL
	INC	CL
	SHR	DX, CL
	XOR	AX, DX
	MOV	DX, AX
	DEC	CL
	SHL	DX, CL
	XOR	AX, DX
	POP	DX
	POP	CX
SEED:
	MOV	XSSTAT, AX
	RET
XSSTAT:
	DW	?
ISSPACE:
	CLC
	CMP	AL, 20H
	JE	SPACE
	CMP	AL, 09H
	JB	NSPACE
	CMP	AL, 0DH
	JA	NSPACE
SPACE:
	STC
NSPACE:
	RET
TRIM:
	PUSH	AX
	MOV	[DS:0080H], AL
	MOV	BX, CX
TRIMCH:
	MOV	AL, [BX+0080H]
	CALL	ISSPACE
	JNC	TRIMQ
	DEC	BX
	LOOP	TRIMCH
TRIMQ:
	MOV	[DS:0080H], CL
	POP	AX
	RET
EMESG1:
	DB	'Error: '
EMESG2:
	DB	': Invalid argument', 0DH, 0AH
	DB	'Usage: RANDGEN [length]', 0DH, 0AH

CODE	ENDS
	END	START
